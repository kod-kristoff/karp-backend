alias dev := install-dev

INVENV := if env_var_or_default('VIRTUAL_ENV', "") == "" { "poetry run" } else { "" }

# setup development environment
install-dev:
	poetry install

# setup CI environment
install-ci: install-dev
	poetry install --only ci

# lint the code
lint *flags="":
    {{INVENV}} ruff {{flags}} src

# type-check the code
type-check:
    {{INVENV}} mypy --config-file mypy.ini -p karp.lex_core

# run formatter
fmt:
	{{INVENV}} black .

# test if code is formatted
check-fmt:
	{{INVENV}} black . --check

# build the project
build:
    poetry build

# bump given part of version
bumpversion part="patch":
	{{INVENV}} bump2version {{part}}

clean: clean-pyc
clean-pyc:
	find . -name '*.pyc' -exec rm -f {} +
	find . -name '*.pyo' -exec rm -f {} +
	find . -name '*~' -exec rm -f {} +
	find . -name '__pycache__' -exec rm -fr {} +
